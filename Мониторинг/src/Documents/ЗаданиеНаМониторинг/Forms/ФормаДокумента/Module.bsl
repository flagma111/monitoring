
&НаСервере
Процедура ВыгрузитьНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Таблица = Объект.Товары.Выгрузить();
	Запрос.УстановитьПараметр("ТЗИсточник", Таблица);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЗ2.Конкурент КАК ТЗКонкурент,
	|	ТЗ2.Номенклатура КАК ТЗНоменклатура,
	|	ТЗ2.Магазин КАК ТЗМагазин,
	|	ТЗ2.Категория КАК ТЗКатегория,
	|	ТЗ2.Цена КАК ТЗЦена
	|ПОМЕСТИТЬ ТЗ
	|ИЗ
	|	&ТЗИсточник КАК ТЗ2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Номенклатура_.Код,
	|	Номенклатура_.Ссылка КАК Товар,
	|	ВЫБОР
	|		КОГДА Номенклатура_.Описание = """"
	|			ТОГДА КатегорииЦен_.Наименование
	|		ИНАЧЕ КатегорииЦен_.Наименование + "" / "" + Номенклатура_.Описание
	|	КОНЕЦ КАК Описание,
	|	Номенклатура_.Родитель КАК Группа,
	|	ТЗ.ТЗЦена КАК БазЦена,
	|	"""" КАК ЦенаПоставщика,
	|	"""" КАК Штрихкод,
	|	"""" КАК Регион,
	|	КаталогФото.Значение + Номенклатура_.Фотография + "".jpg"" КАК СсылкаНаФото,
	|	Магазины_.Код КАК МагазинКод,
	|	ВЫРАЗИТЬ(Конкуренты_.Код КАК СТРОКА) КАК КонкурентКод,
	|	&ДатаНачала,
	|	&ДатаОкончания
	|ИЗ
	|	ТЗ КАК ТЗ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Магазины КАК Магазины_
	|		ПО ТЗ.ТЗМагазин = Магазины_.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Конкуренты КАК Конкуренты_
	|		ПО ТЗ.ТЗКонкурент = Конкуренты_.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура_
	|		ПО ТЗ.ТЗНоменклатура = Номенклатура_.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КатегорииЦен КАК КатегорииЦен_
	|		ПО ТЗ.ТЗКатегория = КатегорииЦен_.Ссылка,
	|	Константа.КаталогФото КАК КаталогФото
	|
	|УПОРЯДОЧИТЬ ПО
	|	Магазины_.Код,
	|	Конкуренты_.Код,
	|	Номенклатура_.Родитель.Родитель.Родитель.Родитель,
	|	Номенклатура_.Родитель.Родитель.Родитель,
	|	Номенклатура_.Родитель.Родитель,
	|	Номенклатура_.Родитель,
	|	Номенклатура_.Код";
	Запрос.УстановитьПараметр("ДатаНачала", Объект.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Объект.ДатаОкончания);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() > 0 Тогда
		
		ТабДокумент = Новый ТабличныйДокумент;
		Макет = Документы.ЗаданиеНаМониторинг.ПолучитьМакет("Выгрузка");
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ТабДокумент.Вывести(ОбластьМакета);

		Пока Выборка.Следующий() Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("Строка");
			ОбластьМакета.Параметры.Код				= Выборка.Код;
			ОбластьМакета.Параметры.Товар			= Выборка.Товар;
			ОбластьМакета.Параметры.Описание		= Выборка.Описание;
			ОбластьМакета.Параметры.Группа			= Выборка.Группа;
			ОбластьМакета.Параметры.БазЦена			= Выборка.БазЦена;
			ОбластьМакета.Параметры.ЦенаПоставщика	= Выборка.ЦенаПоставщика;
			ОбластьМакета.Параметры.Штрихкод		= Выборка.Штрихкод;
			ОбластьМакета.Параметры.СсылкаНаФото	= Выборка.СсылкаНаФото;
			ОбластьМакета.Параметры.Регион			= Выборка.Регион;
			ОбластьМакета.Параметры.МагазинКод		= Выборка.МагазинКод;
			ОбластьМакета.Параметры.КонкурентКод	= Выборка.КонкурентКод;
			ОбластьМакета.Параметры.ДатаНачала		= Выборка.ДатаНачала;
			ОбластьМакета.Параметры.ДатаОкончания	= Выборка.ДатаОкончания;
			ТабДокумент.Вывести(ОбластьМакета);
			
		КонецЦикла;	
		
		ВременныйФайл = ПолучитьИмяВременногоФайла() + ".xls";
		ТабДокумент.Записать(ВременныйФайл, ТипФайлаТабличногоДокумента.XLS);
		ИмяФайла = "gm_items_" + Объект.Номер + ".xls";		
		
		ОбменыДанными.ОтправитьФайлНаФТП(ВременныйФайл, ИмяФайла);
		Объект.Выгружен = Истина;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	ВыгрузитьНаСервере();
КонецПроцедуры
